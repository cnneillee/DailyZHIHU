def buglyAppID = ''
def fabricApiKey = ''

def debugKeystorePath = ''
def debugKeystorePwd = ''
def debugKeystoreAlias = ''
def debugKeystoreAliasPwd = ''

def releaseKeystorePath = ''
def releaseKeystorePwd = ''
def releaseKeystoreAlias = ''
def releaseKeystoreAliasPwd = ''

Properties properties = new Properties()
properties.load(new FileInputStream(new File("private-keys.properties")))
debugKeystorePath = properties.getProperty("keystore.debug.path")
releaseKeystorePath = properties.getProperty("keystore.release.path")

if (debugKeystorePath && releaseKeystorePath) {
    buglyAppID = properties.getProperty("bugly.AppID")
    fabricApiKey = properties.getProperty("fabric.apiKey")

    debugKeystorePwd = properties.getProperty("keystore.debug.storePassword")
    debugKeystoreAlias = properties.getProperty("keystore.debug.keyAlias")
    debugKeystoreAliasPwd = properties.getProperty("keystore.debug.keyPassword")

    releaseKeystorePwd = properties.getProperty("keystore.release.storePassword")
    releaseKeystoreAlias = properties.getProperty("keystore.release.keyAlias")
    releaseKeystoreAliasPwd = properties.getProperty("keystore.release.keyPassword")
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.greenrobot.greendao'
apply from: 'tinker-support.gradle'
apply from: 'multiple-channel.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
            storeFile file(debugKeystorePath)
            storePassword debugKeystorePwd
            keyAlias debugKeystoreAlias
            keyPassword debugKeystoreAliasPwd
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
            storeFile file(releaseKeystorePath)
            storePassword releaseKeystorePwd
            keyAlias releaseKeystoreAlias
            keyPassword releaseKeystoreAliasPwd
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            manifestPlaceholders = [fabric_api_key: fabricApiKey, bugly_app_id: buglyAppID]
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            // 移除无用的resource文件
            shrinkResources true
            manifestPlaceholders = [fabric_api_key: fabricApiKey, bugly_app_id: buglyAppID]
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        warning 'InvalidPackage'
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
    dependencies {

        compile fileTree(include: ['*.jar'], dir: 'libs')
        testCompile 'junit:junit:4.12'

        // base
        compile rootProject.ext.dependencies["appcompat-v7"]
        compile rootProject.ext.dependencies["design"]
        compile rootProject.ext.dependencies["cardview-v7"]

        // network
        compile rootProject.ext.dependencies["gson"]
        compile rootProject.ext.dependencies["volley"]
        compile rootProject.ext.dependencies["okhttp"]
        compile rootProject.ext.dependencies["retrofit"]
        compile rootProject.ext.dependencies["retrofit-converter-gson"]
        compile rootProject.ext.dependencies["universal-image-loader"]

        // ui
        compile rootProject.ext.dependencies["android-observablescrollview"]
        compile rootProject.ext.dependencies["nineoldandroids"]
        compile rootProject.ext.dependencies["circleimageview"]
        compile rootProject.ext.dependencies["material-calendarview"]
        compile('cn.trinea.android.view.autoscrollviewpager:android-auto-scroll-view-pager:1.1.2') {
            exclude module: 'support-v4'
        }
//        compile 'com.mikepenz:actionitembadge:3.3.1@aar'

        // log
        compile rootProject.ext.dependencies["logger"]

        // di
        compile rootProject.ext.dependencies["dagger"]
        provided rootProject.ext.dependencies["dagger-compiler"]
        provided rootProject.ext.dependencies["annotation"]
        compile rootProject.ext.dependencies["butterknife"]
        apt rootProject.ext.dependencies["butterknife-compiler"]

        // rx
        compile rootProject.ext.dependencies["rxjava"]
        compile rootProject.ext.dependencies["rxandroid"]

        // db
        compile 'org.greenrobot:greendao:3.2.2'

        compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
            transitive = true;
        }

        compile "com.android.support:multidex:1.0.1" // 多dex配置

        // walle（多渠道使用）
        compile 'com.meituan.android.walle:library:1.1.3'

        // bugly
        // 注释掉原有bugly的仓库
        // compile rootProject.ext.dependencies["crashreport"]
        // compile 'com.tencent.bugly:crashreport:latest.release'//其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.3.2
        compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
        //其中latest.release指代最新版本号，也可以指定明确的版本号，例如1.2.0
        compile 'com.tencent.bugly:nativecrashreport:latest.release'
        //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0

        //canary
        debugCompile rootProject.ext.dependencies["blockcanary-android"]
        releaseCompile rootProject.ext.dependencies["blockcanary-no-op"]
        debugCompile rootProject.ext.dependencies["leakcanary-android"]
        releaseCompile rootProject.ext.dependencies["leakcanary-android-no-op"]
    }
}

